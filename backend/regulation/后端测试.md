# 后台测试说明

目前后台测试包括单元测试与集成测试，对此做一下说明：

- 单元测试

  针对程序模块（软件设计的最小单位）来进行正确性检验的测试工作。程序单元是应用的最小可测试部件。在过程化编程中，一个单元就是单个程序、函数、过程等；对于面向对象编程，最小单元就是方法，包括基类（超类）、抽象类、或者派生类（子类）中的方法

- 集成测试

  整合测试又称组装测试，即对程序模块采用一次性或增值方式组装起来，对系统的接口进行正确性检验的测试工作。整合测试一般在单元测试之后、系统测试之前进行。

## 所用技术

- [Spock](http://spockframework.org/spock/docs/1.3-RC1/index.html):测试框架
- Groovy:所用语言（与 java 兼容，可以混用）
- [H2](http://www.h2database.com/html/main.html):嵌入式数据库，用于替代 MySQL，**_集成测试使用_**
- [embedded-redis](https://github.com/kstyrc/embedded-redis):嵌入式 redis，**_集成测试使用_**
- [wiremock](http://wiremock.org/):Api Mock 工具，用于拦截对外请求，**_集成测试使用_**
- [gradle-testsets-plugin](https://github.com/unbroken-dome/gradle-testsets-plugin):gradle 插件，用于区分单元测试与集成测试

## 整体说明

### 区分两种测试

项目引入`gradle-testsets-plugin`插件，用于区分单元测试与集成测试，做法如下：

1. 引入插件

   在 buildscript -> dependencies 中加入如下语句

   ```groovy
   classpath( 'org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:1.4.2')
   ```

2. 区分两种测试

   在`gradle`文件中加入如下配置，用于标志区分两种测试

   ```groovy
   // 单元测试与集成测试的相关配置
   apply plugin: 'org.unbroken-dome.test-sets'

   testSets {
       // 指定集成测试的目录
       integrationTest { dirName = 'test/integration' }
   }

   check.dependsOn integrationTest
   integrationTest.mustRunAfter test

   // 单元测试的配置，必须使用test task，指定了单元测试的category
   test {
       useJUnit {
           includeCategories 'com.tehang.resource.train.UnitTest'
       }
       testLogging {
           showStandardStreams = true
       }
   }

   // 自定义的集成测试task，指定了集成测试的category

   integrationTest {
       useJUnit {
           includeCategories 'com.tehang.resource.train.IntegrationTest'
       }
       testLogging {
           showStandardStreams = true
       }
   }
   ```

   - `test/integration`是指定的集成测试代码的目录，而单元测试代码仍然放在`test/groovy`目录下，这是默认目录，不需要指定

   - `com.tehang.resource.train.UnitTest`是单元测试使用的标志接口，所有单元测试都实现这个接口，实际做法是有一个`UnitTestSpecification`实现这个接口，然后继承这个类

   - `com.tehang.resource.train.IntegrationTest`是集成测试使用的标志接口，所有集成测试都实现这个接口，实际做法是有一个`IntegrationTestSpecification`实现这个接口，然后继承这个类

3. 结构说明

仅对`test`目录说明

```text
test
  |-groovy 单元测试代码所在目录，内部为package结构，与程序对应
  |-integration
    |-groovy 集成测试代码所在目录，内部为package结构，与程序对应
    |-resources 继承测试使用的配置或资源
  |-resources 单元测试使用的配置或者资源
```

PS:问题点

## 如何写单元测试

1. 新建`groovy`类，继承`UnitTestSpecification`

2. Mock 该类依赖的外部类

3. 构建数据，编写测试用例

例子：

```groovy
class ApprovalRejectDomainServiceSpec extends UnitTestSpecification {

  ApprovalRepository approvalRepo = Mock(ApprovalRepository)
  FlightOrderRepository orderRepo = Mock(FlightOrderRepository)
  FlightTaskRepository taskRepo = Mock(FlightTaskRepository)
  FlightOrderCancelConfirmDomainService cancelConfirmDomainService = Mock(FlightOrderCancelConfirmDomainService)

  ApprovalAuditRejectDomainService auditRejectDomainService =
          new ApprovalAuditRejectDomainService(approvalRepo, orderRepo, cancelConfirmDomainService, taskRepo)

  def loadFromJson(String fileUrl) {
    def json = new String(new File(fileUrl).getBytes())
    Approval approval = com.tehang.tmc.services.utility.JsonUtils.toClass(json, Approval.class)
    approval.getApprovalHis().each {
      his ->
        his.setApproval(approval)
    }
    return approval
  }

  def "审批拒绝测试"() {
    given: "给定审批参数"

    long employeeId = 1
    String employeeName = ""
    Approval approval = loadFromJson("src/test/resources/json/corp/approval/approval_simple.json")
    ApprovalAuditBo bo = new ApprovalAuditBo()
    bo.setAuditPassed(false)

    orderRepo.getFlightOrdersByApprovalId(_) >> Arrays.asList()

    when: "审批拒绝"
    auditRejectDomainService.auditReject(employeeId, employeeName, bo, approval)

    then: "审批单状态为已拒绝"
    approval.status == ApprovalStatus.REJECTED
  }

}
```

## 集成测试

集成测试相对于单元测试，是更进一步的测试，会把项目运行起来，然后把内部所有模块都集成起来测试，需要满足以下条件：

1. 不依赖于外部服务，能独立完成所有测试

   目前我们使用外部服务，只要是通过发送 http 请求以及 MQ 通讯，其中 http 请求使用 WireMock 来进行拦截以及模拟返回，而 MQ 则使用 MockBean 把 producer 和 consumer 都 mock 起来

2. 每次运行都是独立的，不会受上一次的影响

   运行间独立主要是数据方面的问题，为此引入嵌入式数据库和缓存，每次运行都是新的环境，避免影响

### 如何写集成测试

1. 新建`groovy`类，继承`IntegrationTestSpecification`

2. 引入 WireMockRule，并且制定需要 stub 的地址

```groovy
String insuranceResponseStr = new FileReader('src/test/resources/json/insurance/insurance.json').text
        stubFor(post(urlPathMatching("/v1/insurance/all"))
                .willReturn(aResponse().withHeader("Content-Type", "application/json")
                .withHeader("Connection", "close")
                .withStatus(200)
                .withBody(insuranceResponseStr)))
```

建议建一个 Stub 类，把这个 stub 放着这个类中，在使用的地方引入即可

```groovy
class FlightStub {

    static void stubForSearchFlightResponse() {
        def searchResponseJson = new FileReader('src/test/resources/json/flight/integration/shopping_response.json').text
        stubFor(post(urlPathMatching("/v1/flight/shopping"))
                .willReturn(aResponse().withHeader("Content-Type", "application/json")
                .withHeader("Connection", "close")
                .withStatus(200)
                .withBody(searchResponseJson)))
    }
}
```

```groovy
def "Test1: search"() {
        given:

        HttpHeaders headers = new HttpHeaders()
        headers.add("Authorization", "Bearer " + loginResponse.body.data.token)

        //1 查询车票
        def searchRequest = slurper.parse(new FileReader('src/test/resources/json/flight/integration/shopping_request.json'))
        HttpEntity httpEntity = new HttpEntity(searchRequest, headers)

        FlightStub.stubForSearchFlightResponse()

        FlightStub.stubForInsurance()

        when:
        flightSearchResponse = restTemplate.exchange("/front/v1/flight/searchFlights", HttpMethod.POST, httpEntity, Object.class)

        then:
        flightSearchResponse.status == 200
        flightSearchResponse.body.code == 0
    }
```

3. 编写测试用例

为了集成度更高，建议直接对接口测试，使用 RestTemplate 直接对接口发起请求

```groovy
@Autowired
    private TestRestTemplate restTemplate

    def "Test0: login"() {
        given:

        def loginBo = slurper.parse(new FileReader('src/test/resources/json/employee/login.json'))

        when:
        loginResponse = restTemplate.postForEntity("/front/v1/employee/login", loginBo, Object.class)

        then:
        loginResponse.status == 200
        loginResponse.body.code == 0
    }
```

## 最佳时间

## 处理静态方法的模拟

根据 Peter Niederwieser (Spock 框架主要作者) 的[回答](https://stackoverflow.com/questions/15824315/mock-static-method-with-groovymock-or-similar-in-spock)，要想模拟 java 代码中的静态方法，必须引入其它依赖。在当前情况下要想测试包含静态方法调用的代码，建议绕过“模拟静态方法”这个点，使用集成测试进行（静态方法在集成测试环境下能正常运行）。
